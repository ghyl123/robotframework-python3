*** Settings ***
Library           Remote    http://127.0.0.1:${PORT}
Suite Setup       Set Log Level    DEBUG

*** Variables ***
${PORT}           8270

*** Test Cases ***
No Arguments
    [Template]    Arguments Should Be Accepted
    ${EMPTY}    No Arguments

Required Arguments
    [Template]    Arguments Should Be Accepted
    some argument    One Argument    some argument
    first argument, second argument    Two Arguments    first argument    second argument
    1, 2, 3 (int), 4, 5    Five Arguments    1    2    ${3}    4    5

Arguments With Default Values
    [Template]    Arguments Should Be Accepted
    one, two, three    Arguments With Default Values    one    two    three
    one, two, 3    Arguments With Default Values    one    two
    one, 2 (int), 3    Arguments With Default Values    one

Variable Number Of Arguments
    [Template]    Arguments Should Be Accepted
    ${EMPTY}    Varargs
    One argument    Varargs    One argument
    Three, arguments, now    Varargs    Three    arguments    now
    1, 2, 3, 4 (int), 5    Varargs    1    2    3    ${4}    5

Required Arguments, Default Values and Varargs
    [Template]    Arguments Should Be Accepted
    Hello, world    Required Defaults And Varargs    Hello
    Hi, tellus    Required Defaults And Varargs    Hi    tellus
    Hello, again, world    Required Defaults And Varargs    Hello    again    world
    1, 2, 3, 4, 5 (int)    Required Defaults And Varargs    1    2    3    4    ${5}

Using Arguments When No Accepted
    [Documentation]    FAIL Keyword 'Remote.No Arguments' expected 0 arguments, got 1.
    No Arguments    not allowed

Too Few Arguments When Using Only Required Args
    [Documentation]    FAIL Keyword 'Remote.One Argument' expected 1 argument, got 0.
    One Argument

Too Many Arguments When Using Only Required Args
    [Documentation]    FAIL Keyword 'Remote.Two Arguments' expected 2 arguments, got 3.
    Two Arguments    too    many    arguments

Too Few Arguments When Using Default Values
    [Documentation]    FAIL Keyword 'Remote.Arguments With Default Values' expected 1 to 3 arguments, got 0.
    Arguments With Default Values

Too Many Arguments When Using Default Values
    [Documentation]    FAIL Keyword 'Remote.Arguments With Default Values' expected 1 to 3 arguments, got 5.
    Arguments With Default Values    this    is    way    too    much

Too Few Arguments When Using Varargs
    [Documentation]    FAIL Keyword 'Remote.Required Defaults And Varargs' expected at least 1 argument, got 0.
    Required Defaults And Varargs

*** Keywords ***
Arguments Should Be Accepted
    [Arguments]    ${expected}    ${keyword}    @{arguments}
    ${actual} =    Run Keyword    ${keyword}    @{arguments}
    Should Be Equal    ${actual}    ${expected}
