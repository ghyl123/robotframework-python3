*** Settings ***
Library  DateTime
Variables  datesandtimes.py

*** Test Cases ***
Valid date
    [Template]  Verification should return true
    2014-06-02 11:59:01.111
    2014-06-02 11:59
    11:59 02-06-2014    %H:%M %d-%m-%Y
    ${datetime.now()}
    ${123}

Invalid date
    [Template]  Verification should return false
    no date here
    11:59 02-06-2014    %S:%H:%M %d-%m-%Y
    ${timedelta(1)}

*** Keywords ***
Verification should return true
    [Arguments]  ${date}  ${input_format}=${NONE}
    ${result} =  Should Be Date  ${date}  ${input_format}
    Should Be True  ${result}

Verification should return false
    [Arguments]  ${date}  ${input_format}=${NONE}
    ${result} =  Should Be Date  ${date}  ${input_format}
    Should Not Be True  ${result}
