*** Settings ***
Test Template     Date Conversion Should Succeed
Library           DateTime
Variables         datesandtimes.py

*** Variables ***
${DATE}           ${datetime(2014, 4, 24, 21, 45, 12, 123000)}

*** Test Cases ***    INPUT                      FORMAT                         OUTPUT                     INPUT FORMAT
Should convert to timestamp
                      2014-04-24 21:45:12.123    timeSTAMP                      2014-04-24 21:45:12.123
                      2014-04-24 21:45:12.123    tImestamp                      2014-04-24 21:45:12.123    %Y-%m-%d %H:%M:%S.%f
                      ${1398365112.0}            TIMEstamp                      2014-04-24 21:45:12.000
                      ${DATE}                    TimeStamp                      2014-04-24 21:45:12.123

Should convert to timestamp with format
                      2014-04-24 21:45:12.123    TimeStamp:%H:%M:%S %Y-%m-%d       21:45:12 2014-04-24
                      2014-04-24 21:45:12.123    TimeStamp:%H:%M:%S.%f %Y-%m-%d    21:45:12.123000 2014-04-24
                      2014/04/24 21:45:12.123    timestamp:%H:%M %Y-%m-%d          21:45 2014-04-24           %Y/%m/%d %H:%M:%S.%f

Should convert to epoch
                      2014-04-24 21:45:12.123    epoch                          ${1398365112.123}
                      2014-04-24 21:45:12.123    epoch                          ${1398365112.123}          %Y-%m-%d %H:%M:%S.%f
                      ${1398365112.0}            epoch                          ${1398365112.0}
                      ${DATE}                    epoch                          ${1398365112.123}

Should convert to datetime
                      2014-04-24 21:45:12.123    datetime                       ${DATE}
                      2014-04-24 21:45:12.123    datetime                       ${DATE}                    %Y-%m-%d %H:%M:%S.%f
                      ${1398365112.123}          dateTiMe                       ${DATE}
                      ${DATE}                    datetimE                       ${DATE}

Should exclude milliseconds
                      [Template]    Date Conversion Should Succeed Without Milliseconds
                      2014-04-24 21:45:12.123    timestamp                      2014-04-24 21:45:12
                      2014-04-24 21:45:12.999    timestamp                      2014-04-24 21:45:13
                      ${DATE}                    timestamp                      2014-04-24 21:45:12
                      ${1398365112.123}          datetime                       ${datetime(2014, 4, 24, 21, 45, 12)}

*** Keywords ***
Date Conversion Should Succeed
    [Arguments]    ${input}    ${output_format}    ${expected}    ${input_format}=${NONE}
    ${ts} =    Convert Date    ${input}    ${output_format}    date_format=${input_format}
    Should Be Equal    ${ts}    ${expected}

Date Conversion Should Succeed Without Milliseconds
    [Arguments]    ${input}    ${output_format}    ${expected}
    ${ts} =    Convert Date    ${input}    ${output_format}    exclude_millis=True
    Should Be Equal    ${ts}    ${expected}
