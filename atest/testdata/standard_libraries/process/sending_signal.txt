*** Settings ***
Test Setup        Run Keywords
...               Check Precondition    os.sep == "/" and sys.version_info >= (2,6)
...               AND    Remove File    ${TEMPFILE}
Suite Teardown    Terminate All Processes
Resource          resource.txt

*** Test Cases ***
Sending INT signal
    Killer signal    INT

Sending SIGINT signal
    Killer signal    SIGINT

Sending INT signal as a text number
    Killer signal    2

Sending INT signal as a number
    Killer signal    ${2}

Send other well-known signals
    :FOR    ${signal}    IN    TERM    SIGTERM    15    KILL    SIGKILL    ${9}
    \    Killer signal    ${signal}

Sending an unknown signal
    [Documentation]    FAIL Unsupported signal 'unknown'.
    Start Python Process    1+1
    Send Signal To Process    unknown

Sending signal to a process with a handle
    ${index} =    Start Countdown
    Start Countdown    alias=alias
    Send Signal To Process    INT    handle=${index}
    Send Signal To Process    INT    alias
    Countdown Should Have Stopped    ${index}
    Countdown Should Have Stopped    alias

Sending signal to a process with a wrong handle
    [Documentation]    FAIL Non-existing index or alias 'unknown'.
    Send Signal To Process    2    handle=unknown

*** Keywords ***
Killer signal
    [Arguments]    ${signal}
    Start Countdown    alias=${signal}
    Send Signal To Process    ${signal}
    Countdown Should Have Stopped    handle=${signal}

Start Countdown
    [Arguments]    ${alias}=    ${shell}=False
    ${handle} =    Start Process    python    ${COUNTDOWN}    ${TEMPFILE}
    ...    alias=${alias}    shell=${shell}
    Wait Until Countdown Started
    [Return]    ${handle}
