*** Settings ***
Resource          resource.txt
Test Teardown     Safe Remove File    ${STDOUT}    ${STDERR}

*** Test Cases ***
Default stdout and stderr
    ${result} =    Run Stdout Stderr Process
    Result Should Equal    ${result}    stdout    stderr

Custom stdout
    ${result} =    Run Stdout Stderr Process    stdout=${STDOUT}
    Result Should Equal    ${result}    stdout    stderr    stdout_path=${STDOUT}

Custom stderr
    ${result} =    Run Stdout Stderr Process    stderr=${STDERR}
    Result Should Equal    ${result}    stdout    stderr    stderr_path=${STDERR}

Custom stdout and stderr
    ${result} =    Run Stdout Stderr Process    stdout=${STDOUT}    stderr=${STDERR}
    Result Should Equal    ${result}    stdout    stderr    stdout_path=${STDOUT}    stderr_path=${STDERR}

Custom stdout and stderr to same file
    ${result} =    Run Stdout Stderr Process    stdout=${STDOUT}    stderr=${STDOUT}
    Result Should Match    ${result}    std???std???    std???std???
    ...    stdout_path=${STDOUT}    stderr_path=${STDOUT}

Redirecting stderr to stdout
    ${result} =    Run Stdout Stderr Process    stderr=STDOUT
    Result Should Match    ${result}    std???std???

Redirecting stderr to custom stdout
    ${result} =    Run Stdout Stderr Process    stdout=${STDOUT}    stderr=STDOUT
    Result Should Match    ${result}    std???std???    std???std???
    ...    stdout_path=${STDOUT}    stderr_path=${STDOUT}

Custom streams are written under cwd when relative
    [Setup]    Create Directory    ${CWD}
    ${result} =    Run Stdout Stderr Process    cwd=${CWD}    stdout=stdout.txt    stderr=stderr.txt
    Result Should Equal    ${result}    stdout    stderr    stdout_path=${CWD}/stdout.txt    stderr_path=${CWD}/stderr.txt
    [Teardown]    Safe Remove Directory    ${CWD}

Cwd does not affect absolute custom streams
    [Setup]    Create Directory    ${CWD}
    ${result} =    Run Stdout Stderr Process    cwd=${CWD}    stdout=${STDOUT}    stderr=${STDERR}
    Result Should Equal    ${result}    stdout    stderr    stdout_path=${STDOUT}    stderr_path=${STDERR}
    [Teardown]    Safe Remove Directory    ${CWD}

*** Keywords ***
Run Stdout Stderr Process
    [Arguments]    ${stdout}=${NONE}    ${stderr}=${NONE}    ${cwd}=${NONE}
    ${code} =    Catenate    SEPARATOR=;
    ...    import sys
    ...    sys.stdout.write('stdout')
    ...    sys.stderr.write('stderr')
    ${result} =    Run Process    python    -c    ${code}
    ...    stdout=${stdout}    stderr=${stderr}    cwd=${cwd}
    [Return]    ${result}
