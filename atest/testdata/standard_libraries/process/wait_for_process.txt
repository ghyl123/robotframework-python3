*** Settings ***
Suite Teardown    Terminate All Processes
Library    Process
Resource   resource.txt

*** Variables ***
${NONTERM}  ${CURDIR}${/}files${/}non_terminable.py

*** Test Cases ***
Wait For Process
    ${process} =    Start Python Process    print 'Robot Framework'
    ${result} =    Wait For Process    ${process}
    Process Should Be Stopped    ${process}
    Should Be Equal As Integers    ${result.rc}    0

Wait For Process Timeout
    ${process} =    Start Python Process    while(True): pass
    Process Should Be Running    ${process}
    ${result} =    Wait For Process    ${process}    timeout=1s
    Process Should Be Running    ${process}
    Should Be Equal    ${result.rc}    ${None}

Wait For Process Terminate On Timeout
    ${process} =   Start Python Process    while(True): pass
    Process Should Be Running    ${process}
    Wait For Process    ${process}    timeout=1s    handle_timeout=terminate
    Process Should Be Stopped    ${process}

Wait For Process Kill On Timeout
    ${process} =   Start Python Process    while(True): pass
    Process Should Be Running    ${process}
    Wait For Process    ${process}    timeout=1s    handle_timeout=kill
    Process Should Be Stopped    ${process}

Wait For Process Kill When Terminate Fails
    ${process} =    Start Process    python    ${NONTERM}
    Process Should Be Running    ${process}
    Wait For Process    ${process}    timeout=1s    handle_timeout=terminate
    Process Should Be Stopped    ${process}