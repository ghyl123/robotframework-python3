*** Settings ***
Resource        atest_resource.txt

*** Variables ***
${TEST SCREENSHOT}    %{TEMPDIR}${/}test-screenshot.jpg

*** Keywords ***
Run if dependencies are available
    [arguments]    @{args}
    Make Suite Non-critical And Fail It If Screenshot dependencies are not installed
    Run Tests      @{args}

Make Suite Non-critical And Fail It If Screenshot dependencies are not installed
    ${dependencies} =     Are Screenshot dependencies available
    Run Keyword If    not ${dependencies}    Fail
    ...   This suite is made non-critical and not executed because Screenshot library's dependencies are not installed or tests are run without display.
    ...   -regression

Are Screenshot dependencies available
    ${dependencies} =   Run keyword if    '${JYTHON}'
    ...    Set variable    ${TRUE}
    ...    ELSE
    ...    Interpreter has screenshot dependencies
    [return]     ${dependencies}

Interpreter has screenshot dependencies
    ${lib} =    Normalize Path    ${ROBOT PATH}/libraries/Screenshot.py
    ${result} =    Run Process   ${INTERPRETER}    ${lib}    test    ${TEST SCREENSHOT}    stderr=STDOUT
    Log    ${result.stdout}
    [teardown]    Remove File    ${TEST SCREENSHOT}
    [return]    ${result.rc == 0}
